import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.sentry.android.gradle'
apply plugin: 'com.heapanalytics.android'

apply from: "../quality/install-git-hook.gradle"

repositories {
  maven { url 'https://jitpack.io' }
}

sentry {
  autoProguardConfig true
  // We can disable auto upload of proguard mappings since we use -dontobfuscate when running proguard
  autoUpload false
}

kapt {
  arguments {
    arg("room.schemaLocation", "$projectDir/schemas".toString())
  }
}

tasks.withType(Test) {
  testLogging {
    // set options for log level LIFECYCLE
    events TestLogEvent.PASSED, TestLogEvent.SKIPPED, TestLogEvent.FAILED, TestLogEvent.STANDARD_OUT
    showExceptions true
    exceptionFormat TestExceptionFormat.FULL
    showCauses true
    showStackTraces true

    // set options for log level DEBUG and INFO
    debug {
      events TestLogEvent.STARTED, TestLogEvent.PASSED, TestLogEvent.SKIPPED, TestLogEvent.FAILED, TestLogEvent.STANDARD_OUT, TestLogEvent.STANDARD_ERROR
      exceptionFormat TestExceptionFormat.FULL
    }
    info.events = debug.events
    info.exceptionFormat = debug.exceptionFormat

    afterSuite { desc, result ->
      if (!desc.parent) { // will match the outermost suite
        def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
        def startItem = '|  ', endItem = '  |'
        def repeatLength = startItem.length() + output.length() + endItem.length()
        println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
      }
    }
  }
}

android {
  compileSdkVersion versions.compileSdk

  defaultConfig {
    applicationId "org.simple.clinic"
    minSdkVersion versions.minSdk
    targetSdkVersion versions.compileSdk
    versionCode 1
    versionName "0.1"
    multiDexEnabled true

    testInstrumentationRunner "org.simple.clinic.AndroidTestJUnitRunner"
    proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
  }

  buildTypes {
    debug {
      ext.heapEnabled = false
      applicationIdSuffix ".debug"
      minifyEnabled false
      useProguard false
      shrinkResources false
    }

    release {
      debuggable false
      ext {
        heapEnabled = true
        heapAutoInit = false
      }
      if (runProguard.equalsIgnoreCase("true")) {
        minifyEnabled true
        shrinkResources true
        useProguard true
      } else {
        minifyEnabled false
        shrinkResources false
        useProguard false
      }
    }
  }

  flavorDimensions 'track'

  productFlavors {
    qa {
      dimension 'track'
      applicationIdSuffix ".qa"
      versionNameSuffix "-qa"
      buildConfigField "String", "API_ENDPOINT", "\"" + qaApiEndpoint + "\""
      buildConfigField "String", "HEAP_ID", "\"" + qaHeapId + "\""
      buildConfigField "String", "MIXPANEL_TOKEN", "\"" + qaMixpanelToken + "\""
      buildConfigField "String", "MASKED_PHONE_NUMBER", "\"" + qaMaskedPhoneNumber + "\""
      buildConfigField "boolean", "DISABLE_SCREENSHOT", "false"
    }

    staging {
      dimension 'track'
      applicationIdSuffix ".staging"
      versionNameSuffix "-demo"
      buildConfigField "String", "API_ENDPOINT", "\"" + stagingApiEndpoint + "\""
      buildConfigField "String", "HEAP_ID", "\"" + stagingHeapId + "\""
      buildConfigField "String", "MIXPANEL_TOKEN", "\"" + stagingMixpanelToken + "\""
      buildConfigField "String", "MASKED_PHONE_NUMBER", "\"" + stagingMaskedPhoneNumber + "\""
      buildConfigField "boolean", "DISABLE_SCREENSHOT", "false"
    }

    sandbox {
      dimension 'track'
      applicationIdSuffix ".sandbox"
      versionNameSuffix "-sandbox"
      buildConfigField "String", "API_ENDPOINT", "\"" + sandboxApiEndpoint + "\""
      buildConfigField "String", "HEAP_ID", "\"" + sandboxHeapId + "\""
      buildConfigField "String", "MIXPANEL_TOKEN", "\"" + sandboxMixpanelToken + "\""
      buildConfigField "String", "MASKED_PHONE_NUMBER", "\"" + sandboxMaskedPhoneNumber + "\""
      buildConfigField "boolean", "DISABLE_SCREENSHOT", "false"
    }

    production {
      dimension 'track'
      buildConfigField "String", "API_ENDPOINT", "\"" + productionApiEndpoint + "\""
      buildConfigField "String", "HEAP_ID", "\"" + productionHeapId + "\""
      buildConfigField "String", "MIXPANEL_TOKEN", "\"" + productionMixpanelToken + "\""
      buildConfigField "String", "MASKED_PHONE_NUMBER", "\"" + productionMaskedPhoneNumber + "\""
      buildConfigField "boolean", "DISABLE_SCREENSHOT", "true"
    }
  }

  variantFilter { variant ->
    if (variant.name == "qaRelease" ||
        variant.name == "stagingDebug" ||
        variant.name == "sandboxDebug" ||
        variant.name == "productionDebug"
    ) {
      setIgnore(true)
    }
  }

  lintOptions {
    lintConfig file("$project.rootDir/quality/lint.xml")
    warningsAsErrors true
    abortOnError true
    checkReleaseBuilds false
  }

  compileOptions {
    targetCompatibility 1.8
    sourceCompatibility 1.8
  }

  androidExtensions {
    experimental = true
  }

  sourceSets {
    def sharedTestDir = 'src/sharedTest/java'
    test {
      java.srcDirs += sharedTestDir
    }
    androidTest {
      assets.srcDirs += files("$projectDir/schemas".toString())
      java.srcDirs += sharedTestDir
    }
  }

  packagingOptions {
    // Deprecated ABIs. See https://developer.android.com/ndk/guides/abis
    exclude 'lib/mips/libsqlite3x.so'
    exclude 'lib/mips64/libsqlite3x.so'
    exclude 'lib/armeabi/libsqlite3x.so'
  }

  // We don't obfuscate (only minify) using proguard. Gradle plugin 3.2.0 (and greater?) generates
  // an empty mappings.txt file. This caused an issue where the CI deploy to play store task tries
  // to upload the empty mapping file, which causes the Play Store api to complain.
  task deleteProguardMappings(type: Delete) {
    delete fileTree(buildDir).matching {
      include "outputs/mapping/**/mapping.txt"
    }
  }

  afterEvaluate {
    tasks.findAll { it.name in ["assembleStagingRelease", "assembleSandboxRelease", "assembleProductionRelease"] }
        .forEach { it.finalizedBy deleteProguardMappings }
  }
}

dependencies {
  implementation project(path: ':router')

  testImplementation "junit:junit:$versions.junit"
  testImplementation "org.mockito:mockito-core:$versions.mockito"
  testImplementation "com.nhaarman:mockito-kotlin:$versions.mockitoKotlin"
  testImplementation "pl.pragmatists:JUnitParams:$versions.junitParams"
  testImplementation "com.google.truth:truth:$versions.truth"
  testImplementation "com.github.blocoio:faker:$versions.faker"

  androidTestImplementation "androidx.annotation:annotation:$versions.supportLib"
  androidTestImplementation "androidx.test:runner:$versions.supportTest"
  androidTestImplementation "androidx.test:rules:$versions.supportTest"
  androidTestImplementation "com.google.truth:truth:$versions.truth"
  androidTestImplementation "com.github.blocoio:faker:$versions.faker"
  androidTestImplementation "androidx.room:room-testing:$versions.room"
  androidTestImplementation "androidx.arch.core:core-testing:$versions.coreTesting"
  kaptAndroidTest "com.google.dagger:dagger-compiler:$versions.dagger"

  debugImplementation "com.facebook.stetho:stetho:$versions.stetho"
  debugImplementation "com.facebook.stetho:stetho-okhttp3:$versions.stetho"

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
  testImplementation "org.jetbrains.kotlin:kotlin-reflect:$versions.kotlin"

  implementation "androidx.appcompat:appcompat:$versions.supportLib"
  implementation "androidx.recyclerview:recyclerview:$versions.recyclerView"
  implementation "com.google.android.material:material:$versions.material"
  implementation "androidx.cardview:cardview:$versions.cardview"
  implementation "androidx.constraintlayout:constraintlayout:$versions.constraintLayout"
  implementation "androidx.room:room-runtime:$versions.room"
  kapt "androidx.room:room-compiler:$versions.room"
  implementation "androidx.room:room-rxjava2:$versions.room"
  implementation "com.google.android.gms:play-services-location:$versions.playServicesLocation"
  implementation "com.google.firebase:firebase-core:$versions.firebaseCore"
  implementation "com.google.firebase:firebase-config:$versions.firebaseConfig"

  implementation "com.jakewharton.timber:timber:$versions.timber"
  implementation "com.google.dagger:dagger:$versions.dagger"
  kapt "com.google.dagger:dagger-compiler:$versions.dagger"
  implementation("com.github.JakeWharton:kotterknife:$versions.kotterKnife") {
    exclude group: 'com.android.support'
  }
  implementation "io.reactivex.rxjava2:rxjava:$versions.rxJava"
  implementation "io.reactivex.rxjava2:rxandroid:$versions.rxAndroid"
  implementation "com.jakewharton.rxbinding2:rxbinding:$versions.rxBinding"
  implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:$versions.rxBinding"
  implementation "io.reactivex.rxjava2:rxkotlin:$versions.rxKotlin"
  implementation "org.threeten:threetenbp:$versions.threeTenBp:no-tzdb"
  implementation "com.gabrielittner.threetenbp:lazythreetenbp:$versions.lazyThreeTenBp"
  implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
  implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofit"
  implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"
  implementation "com.squareup.retrofit2:converter-scalars:$versions.retrofit"
  implementation "com.squareup.okhttp3:logging-interceptor:$versions.okLoggingInterceptor"
  implementation "com.squareup.moshi:moshi:$versions.moshi"
  kapt "com.squareup.moshi:moshi-kotlin-codegen:$versions.moshi"
  implementation "com.squareup.moshi:moshi-adapters:$versions.moshi"
  implementation "com.f2prateek.rx.preferences2:rx-preferences:$versions.rxPreference"
  implementation "com.tspoon.traceur:traceur:$versions.traceur"
  implementation "ru.egslava:MaskedEditText:$versions.maskedEditText"
  implementation "io.sentry:sentry-android:$versions.sentry"
  implementation("org.slf4j:slf4j-nop:$versions.slf4j") {
    because "sentry-android needs this dependency"
  }
  implementation "com.xwray:groupie:$versions.groupie"
  implementation "com.xwray:groupie-kotlin-android-extensions:$versions.groupie"
  implementation "com.mikepenz:itemanimators:$versions.itemAnimators@aar"
  implementation "org.mindrot:jbcrypt:$versions.jbcrypt"
  implementation "com.squareup.flow:flow:$versions.flow"
  implementation "io.requery:sqlite-android:$versions.sqliteAndroid"

  implementation "com.google.android.gms:play-services-auth:$versions.playServicesAuth"
  implementation "io.github.inflationx:viewpump:$versions.viewPump"
  implementation "com.heapanalytics.android:heap-android-client:$versions.heap"
  implementation "com.facebook.shimmer:shimmer:$versions.fbShimmer"
  implementation "com.alimuzaffar.lib:pinentryedittext:$versions.pinEntryEditText"
  implementation "info.debatty:java-string-similarity:$versions.javaStringSimilarity"
  androidTestImplementation "androidx.test.espresso:espresso-core:$versions.espresso"
  debugImplementation "com.squareup.leakcanary:leakcanary-android:$versions.leakCanary"
  debugImplementation "com.squareup.leakcanary:leakcanary-support-fragment:$versions.leakCanary"

  implementation "com.budiyev.android:code-scanner:$versions.codeScanner"
  implementation "com.google.android.play:core:$versions.playCore"

  implementation "com.simplecityapps:recyclerview-fastscroll:$versions.fastScroll"
  implementation "com.google.android:flexbox:$versions.flexbox"
  implementation "com.mixpanel.android:mixpanel-android:$versions.mixpanel"

  implementation "androidx.work:work-runtime:$versions.workManager"
  implementation "androidx.work:work-gcm:$versions.workManager"

  implementation "com.spotify.mobius:mobius-core:$versions.mobius"
  implementation("com.spotify.mobius:mobius-rx2:$versions.mobius") {
    exclude group: "io.reactivex.rxjava2"
    because "Traceur is incompatible with the version of RxJava this artifact pulls in transitively"
  }
  implementation "com.spotify.mobius:mobius-android:$versions.mobius"
  implementation "com.spotify.mobius:mobius-extras:$versions.mobius"
  testImplementation "com.spotify.mobius:mobius-test:$versions.mobius"
  testImplementation(project(":mobius-migration")) {
    exclude group: "io.reactivex.rxjava2"
    because "Traceur is incompatible with the version of RxJava this artifact pulls in transitively"
  }
}

// This must always be present at the bottom of this file, as per:
// https://console.firebase.google.com/u/2/project/simple-org/settings/general/
apply plugin: 'com.google.gms.google-services'
