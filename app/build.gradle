apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.sentry.android.gradle'

apply from: "../quality/install-git-hook.gradle"

repositories {
  maven { url 'https://jitpack.io' }
}

sentry {
  autoProguardConfig true
  // We can disable auto upload of proguard mappings since we use -dontobfuscate when running proguard
  autoUpload false
}

android {
  compileSdkVersion versions.compileSdk

  defaultConfig {
    applicationId "org.simple.clinic"
    minSdkVersion versions.minSdk
    targetSdkVersion versions.compileSdk
    versionCode 1
    versionName "0.1"
    multiDexEnabled true

    testInstrumentationRunner "org.simple.clinic.AndroidTestJUnitRunner"
    proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
  }

  buildTypes {
    debug {
      applicationIdSuffix ".debug"
      minifyEnabled false
      useProguard false
      shrinkResources false
    }

    release {
      debuggable false

      if (runProguard.equalsIgnoreCase("true")) {
        minifyEnabled true
        shrinkResources true
        useProguard true
      } else {
        minifyEnabled false
        shrinkResources false
        useProguard false
      }
    }
  }

  flavorDimensions 'track'

  productFlavors {
    qa {
      dimension 'track'
      applicationIdSuffix ".qa"
      versionNameSuffix "-qa"
      buildConfigField "String", "API_ENDPOINT", "\"" + qaApiEndpoint + "\""
    }

    staging {
      dimension 'track'
      applicationIdSuffix ".staging"
      versionNameSuffix "-staging"
      buildConfigField "String", "API_ENDPOINT", "\"" + stagingApiEndpoint + "\""
    }

    production {
      dimension 'track'
      buildConfigField "String", "API_ENDPOINT", "\"" + productionApiEndpoint + "\""
    }
  }

  variantFilter { variant ->
    if (variant.name == "qaRelease" ||
        variant.name == "stagingDebug" ||
        variant.name == "productionDebug"
    ) {
      setIgnore(true)
    }
  }

  lintOptions {
    lintConfig file("$project.rootDir/quality/lint.xml")
    warningsAsErrors true
    abortOnError true
    checkReleaseBuilds false
  }

  compileOptions {
    targetCompatibility 1.8
    sourceCompatibility 1.8
  }

  androidExtensions {
    experimental = true
  }
}

dependencies {
  implementation project(path: ':router')

  testImplementation 'junit:junit:4.12'
  testImplementation 'org.mockito:mockito-core:2.18.3'
  testImplementation 'com.nhaarman:mockito-kotlin:1.5.0'
  testImplementation 'pl.pragmatists:JUnitParams:1.1.1'
  testImplementation 'com.google.truth:truth:0.40'
  testImplementation 'com.github.blocoio:faker:1.2.7'

  androidTestImplementation 'com.android.support:support-annotations:27.1.1'
  androidTestImplementation "com.android.support.test:runner:$versions.supportTest"
  androidTestImplementation "com.android.support.test:rules:$versions.supportTest"
  androidTestImplementation 'com.google.truth:truth:0.40'
  androidTestImplementation 'com.github.blocoio:faker:1.2.7'
  kaptAndroidTest "com.google.dagger:dagger-compiler:$versions.dagger"

  debugImplementation "com.facebook.stetho:stetho:$versions.stetho"
  debugImplementation "com.facebook.stetho:stetho-okhttp3:$versions.stetho"

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
  testImplementation "org.jetbrains.kotlin:kotlin-reflect:$versions.kotlin"

  implementation "com.android.support:appcompat-v7:$versions.supportLib"
  implementation "com.android.support:recyclerview-v7:$versions.supportLib"
  implementation "com.android.support:design:$versions.supportLib"
  implementation "com.android.support:cardview-v7:$versions.supportLib"
  implementation "android.arch.persistence.room:runtime:$versions.room"
  kapt "android.arch.persistence.room:compiler:$versions.room"
  implementation "android.arch.persistence.room:rxjava2:$versions.room"
  implementation "android.arch.work:work-runtime-ktx:1.0.0-alpha02"

  implementation "com.jakewharton.timber:timber:$versions.timber"
  implementation "com.google.dagger:dagger:$versions.dagger"
  kapt "com.google.dagger:dagger-compiler:$versions.dagger"
  implementation('com.github.JakeWharton:kotterknife:e157638df1') {
    exclude group: 'com.android.support'
  }
  implementation 'io.reactivex.rxjava2:rxjava:2.1.9'
  implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
  implementation 'com.jakewharton.rxbinding2:rxbinding:2.1.1'
  implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'
  implementation 'com.github.nisrulz:qreader:2.1.2'
  implementation 'com.jcabi:jcabi-xml:0.21.4'
  implementation "org.threeten:threetenbp:1.3.6:no-tzdb"
  implementation "com.gabrielittner.threetenbp:lazythreetenbp:0.3.0"
  implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
  implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofit"
  implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"
  implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
  implementation "com.squareup.moshi:moshi:$versions.moshi"
  kapt "com.squareup.moshi:moshi-kotlin-codegen:$versions.moshi"
  implementation 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0'
  implementation 'com.tspoon.traceur:traceur:1.0.1'
  implementation 'ru.egslava:MaskedEditText:1.0.5'
  implementation "io.sentry:sentry-android:$versions.sentry"
  implementation "com.xwray:groupie:$versions.groupie"
  implementation "com.xwray:groupie-kotlin-android-extensions:$versions.groupie"
  implementation "com.mikepenz:itemanimators:1.0.2@aar"
  implementation "org.mindrot:jbcrypt:0.3m"
  implementation "com.squareup.flow:flow:$versions.flow"
}
